Language Specification - CS355 Compiler Project - Ethan Rutherford and Roger Gee
--------------------------------------------------------------------------------
This document describes the specification for the 'Ramsey' language.
--------------------------------------------------------------------------------
A. Keywords

   - keywords are lowercase

[keywords]
   in, boo, fun, endfun, toss, and, or, not, if, elf, endif, true, false, while,
 smash, endwhile
--------------------------------------------------------------------------------
B. Preprocessing

   - Comments: we replace each comment with a newline token
   - We compile a set of preprocessing tokens as an implementation strategy;
these preprocessing tokens are translated into lexical tokens at a later time.
--------------------------------------------------------------------------------
C. Lexical Conventions

   - here we present conventions for grammar productions
   - we use (?) to mean that we are considering adding the convention

token:
        identifier
        keyword
        literal
        operator
        punctuator

digit: one of
        0 1 2 3 4 5 6 7 8 9

hex-digit: one of
        0 1 2 3 4 5 6 7 8 9 A B C D E F a b c d e f

non-digit: one of
        a b c d e f g h i j k l m n o p q r s t u v w x y z A B C D E F G H I J K
L M N O P Q R S T U V W X Y Z _

literal:
        integer-literal
        character-literal (?)
        string-literal (?)
        boolean-literal

integer-literal:
        decimal-literal
        hexadecimal-literal (?)

decimal-literal:
        digit
        decimal-literal digit

hexadecimal-literal:
        0x hex-digit
        0X hex-digit
        hexadecimal-literal hex-digit

boolean-literal:
        true
        false

identifier:
        non-digit
        identifier nondigit
        identifier digit

keyword:
        if
        elf
        endif
        fun
        as
        toss
        endfun
        while
        smash
        endwhile
        in
        big
        small
        boo
        mod
        and
        or
        not

operator:
        + - * / < > <= >= = != <-

punctuator:
        newline
        ,
        ( )


--Note: in this grammar, a single newline token counts for any sequence of newline tokens, though in
-- implementation, newline tokens will represent the logical line count for the input file
newline                                 --we count LFs as logical lines for error reporting
        (ASCII) 0x0A
        (ASCII) 0x0D
        (ASCII) 0x0A newline
        (ASCII) 0x0D newline
--------------------------------------------------------------------------------
D. Grammar

   - here we present a context-free grammar for Ramsey; this grammar employs
lexical conventions defined in the previous section (B)

   - conventions used in this specification:
     <nonterminal>          grammar element that is a non-terminal construct
     [terminal]             grammar element that is one of a class of terminal constructs (usually a token)
     "literal-terminal"     grammar element that is a terminal construct exactly as seen
     ""                     empty element (epsilon)

<program>:                                --a program is one or more functions as we only
        <function-list>                   --allow functions at file level

<function-list>
        <function> <function-list>
        ""

<function>:
        <function-declaration> <statement-list> "endfun" [newline]

<function-declaration>:
        "fun" [identifier] "(" <parameter-declaration> ")" <function-type-specifier> [newline]

<function-type-specifier>:
        "as" <typename>       --explicit function typing
        ""                    --default "in" type function

<parameter-declaration>:
        <parameter> <parameter-list>
        ""

<parameter>:
        <typename> [identifier]

<parameter-list>:
        "," <parameter> <parameter-list>
        ""

<statement>:                                    --each statement kind is separated by a newline
        <declaration-statement>
        <expression-statement>
        <selection-statement>
        <iterative-statement>
        <jump-statement>

<statement-list>:
        <statement> <statement-list>
        ""

<declaration-statement>:
        <typename> [identifier] <initializer> [newline]

<typename>:
        "in"
        "big"
        "small"
        "boo"

<initializer>:
        <assignment-operator> <expression>
        ""

<assignment-operator>:
        "<-"

<expression-statement>:                 --allow sequences of expressions in a single expression statement
        <expression-list> [newline]     --e.g. i<-0, j<-1

<expression>:
        <assignment-expression>

<expression-list>:                      --semantically, this returns the last item in the list
        <expression> <expression-list-item>

<expression-list-item>:
        "," <expression-list>
        ""

<assignment-expression>:
        <logical-or-expression> <assignment-expression-opt>

<assigment-expression-opt>:             --enforce a right-most evaluation (e.g. i = j = 5, do j = 5 first)
        <assignment-operator> <assignment-expression>
        ""

<logical-or-expression>:
        <logical-and-expression> <logical-or-expression-opt>

<logical-or-expression-opt>:
        "or" <logical-and-expression> <logical-or-expression-opt>
        ""

<logical-and-expression>:
        <equality-expression> <logical-and-expression-opt>

<logical-and-expression-opt>:
        "and" <equality-expression> <logical-and-expression-opt>
        ""

<equality-expression>:
        <relational-expression> <equality-expression-opt>

<equality-expression-opt>:
        "=" <relational-expression> <equality-expression-opt>
        "!=" <relational-expression> <equality-expression-opt>
        ""

<relational-expression>:
        <additive-expression> <relational-expression-opt>

<relational-expression-opt>:
        "<" <additive-expression> <relational-expression-opt>
        ">" <additive-expression> <relational-expression-opt>
        "<=" <additive-expression> <relational-expression-opt>
        ">=" <additive-expression> <relational-expression-opt>
        ""

<additive-expression>:
        <multiplicative-expression> <additive-expression-opt>

<additive-expression-opt>:
        "+" <multiplicative-expression> <additive-expression-opt>
        "-" <multiplicative-expression> <additive-expression-opt>
        ""

<multiplicative-expression>:
        <prefix-expression> <multiplicative-expression-opt>

<multiplicative-expression-opt>:
        "*" <prefix-expression> <multiplicative-expression-opt>
        "/" <prefix-expression> <multiplicative-expression-opt>
        "mod" <prefix-expression> <multiplicative-expression-opt>
        ""

<prefix-expression>:
        <postfix-expression>
        "-" <prefix-expression>
        "not" <prefix-expression>

<postfix-expression>:
        <primary-expression> <postfix-expression-opt>

<postfix-expression-opt>:
        "(" <function-call> ")"          --function calls
        ""

<function-call>:
        <expression-list>
        ""

<primary-expression>:
        [literal]
        [identifier]
        "(" <expression> ")"

<selection-statement>:
        "if" "(" <condition> ")" [newline] <if-body> <if-concluder>

<if-body>:
        <statement-list> <elf-body>

<elf-body>
        "elf" "(" <condition> ")" [newline] <if-body>
        ""

<if-concluder>:
        "endif" [newline]
        "else" [newline] <statement-list> "endif" [newline]

<condition>:                                  --just an alias for expression
        <expression>

<iterative-statement>:
        "while" "(" <condition> ")" [newline] <statement-list> "endwhile" [newline]

<jump-statement>:
        "toss" <expression> [newline]                   --note, there is no concept of void, so we demand that something need be returned
        "smash" [newline]                               --a Ramsefied version of break; ends the execution of the nearest loop
--------------------------------------------------------------------------------
